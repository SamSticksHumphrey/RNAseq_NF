process GUNZIP {
    label 'process_low'

    input:
    tuple val(meta), path(archive)

    output:
    tuple val(meta), path("$gunzip"), emit: gunzip
    path "versions.yml"             , emit: versions

    script:

    gunzip = archive.toString() - '.gz'
    """
    gunzip \\
        -f \\
        $archive

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        gunzip: \$(echo \$(gunzip --version 2>&1) | sed 's/^.*(gzip) //; s/ Copyright.*\$//')
    END_VERSIONS
    """

    stub:
    gunzip = archive.toString() - '.gz'
    """
    touch $gunzip
    touch versions.yml
    """
}
